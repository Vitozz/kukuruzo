# $Id$

pkgname=psi-git
pkgver=1.5.1802.gb22e02a2
pkgrel=1
pkgdesc="Psi is a free instant messaging application designed for the XMPP network. Fast and lightweight, Psi is fully open-source and compatible with Windows, Linux, and macOS."
url="https://psi-im.org/"
license=('GPL2')
arch=('x86_64')
depends=('qt6-base' 'qt6-webengine' 'qt6-multimedia'
	 'libidn' 'libxss' 'qt6-svg' 'hunspell' 'qtkeychain-qt6')
makedepends=('git' 'cmake')
provides=("psi=$pkgver" "psi-qt6-git=$pkgver")
conflicts=('psi' 'psi-qt6-git' 'psi-webkit-qt6-git' 'psi-webkit-git')
source=("git+https://github.com/psi-im/psi")
sha256sums=('SKIP')

pkgver() {
  cd ${srcdir}/psi && git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  cd ${srcdir}/psi
  git submodule init
  git submodule update
  mkdir -p ${srcdir}/build
}

build() {
  cmake_flags="-DCMAKE_INSTALL_PREFIX=/usr -DUSE_X11=OFF -DBUNDLED_IRIS_ALL=ON -DUSE_X11=OFF -DCHAT_TYPE=webengine -DINSTALL_PLUGINS_SDK=ON -DQT_DEFAULT_MAJOR_VERSION=6 -DCMAKE_BUILD_TYPE=RelWithDebInfo"
  cd ${srcdir}/build
  cmake ${cmake_flags} -S ${srcdir}/psi "-DVERBOSE_PROGRAM_NAME=ON"
  cmake --build ${srcdir}/build --target all
}

package() {
  cd ${srcdir}/build && make DESTDIR="$pkgdir" install
}
