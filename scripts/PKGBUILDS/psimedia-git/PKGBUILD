# $Id: PKGBUILD 194152 2016-10-31 13:48:24Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vladimir Kutyavin <vlkut-at-bk-dot-ru>

pkgname=psimedia
pkgver=1.0.5.157.gfda9f7c
pkgrel=1
pkgdesc="Voice and video chat plugin for Psi-like IM clients.  The implementation is based on GStreamer."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://delta.affinix.com/psimedia/"
depends=('psi-plus' 'opus' 'gst-plugins-base' 'qt5-base' 'gst-plugins-good')
makedepends=('pkgconfig' 'gdb' 'git')
source=("git://github.com/psi-im/psimedia.git")
md5sums=('SKIP')

pkgver() {
  cd psimedia
  git describe --long --tags | sed 's/^v//;s/-/./g'
}  

build() {
  cmake_flags="-DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo -DBUILD_DEMO=OFF -DUSE_PSI=OFF"
  cd psimedia
  mkdir -p build && cd build
  cmake ${cmake_flags} ..
  make
}

package() {
  cd psimedia/build

  make DESTDIR="$pkgdir" install
}
