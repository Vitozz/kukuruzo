# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>

pkgname=psi-plus-plugins-git
pkgver=1.1.218.g5dc2190
pkgrel=1
pkgdesc="Additional plugins for Psi+ (built with Qt 5.x)"
arch=('x86_64')
url="http://psi-plus.com"
license=('GPL2')
depends=('qt5-base' 'qt5-multimedia' 'psi-plus-git' 'qt5-webkit')
conflicts=('psi-plus-plugins' 'psi-plus-plugins-qt5-git')
provides=("psi-plus-plugins=$pkgver" "psi-plus-plugins-qt5-git=$pkgver")
makedepends=('libotr' 'tidyhtml')
optdepends=('libotr: for OTR plugin'
            'tidyhtml: for OTR plugin')
source=('git://github.com/psi-im/plugins')
md5sums=('SKIP')

pkgver() {
  cd plugins
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

build() {
  cmake_flags="-DCMAKE_INSTALL_PREFIX=/usr -DBUILD_DEV_PLUGINS=ON -DCMAKE_BUILD_TYPE=RelWithDebInfo -DIS_PSIPLUS=ON"
  cd plugins
  mkdir -p build && cd build
  cmake ${cmake_flags}  ..
  make
}

package() {
  cd plugins/build

  make DESTDIR="$pkgdir" install
}

